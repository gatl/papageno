if("${PAPAGENO_TEST_EXECUTABLE}" STREQUAL "")
   message(FATAL_ERROR "No PAPAGENO_TEST_EXECUTABLE provided")
endif()

 if("${PAPAGENO_COMPRESSED_CODE_FILE}" STREQUAL "")
   message(FATAL_ERROR "No PAPAGENO_COMPRESSED_CODE_FILE provided")
endif()  

if(NOT EXISTS "${PAPAGENO_TEST_EXECUTABLE}")
   message("Cannot find test executable ${PAPAGENO_TEST_EXECUTABLE}")
   
   if(EXISTS "${PAPAGENO_COMPRESSED_CODE_FILE}")
      file(REMOVE "${PAPAGENO_COMPRESSED_CODE_FILE}")
   endif()
endif()

if(NOT "${PAPAGENO_RUN_WRAPPER}" STREQUAL "")
   set(output_file "${PAPAGENO_COMPRESSED_CODE_FILE}.tmp")
   set(file_indicator ERROR_FILE)
else()
   set(output_file "${PAPAGENO_COMPRESSED_CODE_FILE}")
   set(file_indicator OUTPUT_FILE)
endif()

set(command ${PAPAGENO_RUN_WRAPPER} "${PAPAGENO_TEST_EXECUTABLE}"
                ${file_indicator} "${output_file}")

message("Executing ${command}")

execute_process(
   COMMAND ${command}
   RESULT_VARIABLE result
)

if((NOT "${result}" EQUAL 0))
   message("Error running ${command}")
   message("result: ${result}")

   if(EXISTS "${output_file}")
      message("Output file content:")
      file(READ "${output_file}" file_content)
      message("${file_content}")
   endif()
   message(FATAL_ERROR "Exit with error")
endif()    
           
if(NOT "${PAPAGENO_RUN_WRAPPER}" STREQUAL "")

   set(command
      COMMAND cat "${output_file}"
      COMMAND sed "s/\\.\\.\$//" # Remove dots at end of the line as generated by simavr
      COMMAND sed "0,/__PPG_START_OF_GENERATED_CODE__/d" # Remove any lines before generated code
      COMMAND sed "/__PPG_END_OF_GENERATED_CODE__/,$ d" # Remove any lines after generated code
      COMMAND sed -r "s/\\x1b\\[([0-9]{1,2}(;[0-9]{1,2})*)?m//g" # Remove coloring escape sequences
   )
   execute_process(
      ${command}
      
      OUTPUT_FILE "${PAPAGENO_COMPRESSED_CODE_FILE}"
      RESULT_VARIABLE result
      OUTPUT_VARIABLE output
      ERROR_VARIABLE error
   )
endif()

if((NOT "${result}" EQUAL 0))

   message("Error running ${command}")
   message("result: ${result}")
   message("output: ${output}")
   message("error: ${error}")
   message(FATAL_ERROR "Exit with error")
endif()